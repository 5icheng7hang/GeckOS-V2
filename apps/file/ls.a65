/****************************************************************************
   
    OS/A65 Version 2.0.0
    lib6502 ls program

    Copyright (C) 1997-1998 Andre Fachat 

    This program is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    This program is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with this program; if not, write to the Free Software
    Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA.

****************************************************************************/

#include "lib6502.i65"

main	.(
	.zero
argp	.word 0
	.text

	; stores argp in a/y to argp var
	jsr getopts
	bcc optsok

	lda #<usagetxt
	ldy #>usagetxt
	jmp strout

optsok	lda (argp),y
	beq jok
	tya
	adc argp
	sta argp
	bcc ok
	inc argp+1
	bne ok

jok	lda #<joker
	sta argp
	lda #>joker
	sta argp+1	; always not null

ok
	jsr crlfout

	lda argp
	ldy argp+1

	jsr fopendir
	bcc opened
	rts

opened	stx fileno
rd	ldx fileno
	lda #<entry
	ldy #>entry
	jsr freaddir
	bcs eof

	bit optlong
	bpl short

	; long output
	jsr longint2bcd
	ldy #9
	jsr bcdout
	jsr spout
short
	lda #<entry+FD_NAME
	ldy #>entry+FD_NAME
	jsr strout

	jsr crlfout
	jmp rd

eof	ldx fileno
	jsr fclose
	lda #0
	rts

	.data

optslst	.asc "l",0
optsflg
optlong	.byt 0

joker	.asc "*", 0
usagetxt
	.asc 13,10,"Usage:",13,10," ls [opts] <dirmask>"
	.asc 13,10,"Options:"
	.asc 13,10,"-l    print more file information"
	.byt 0

	.bss
fileno	.byt 0
entry	.dsb FD_NAME+MAX_FILENAME
BIN	=entry+FD_LENGTH
BCD	.byt 0,0,0,0,0
	.text

#include ../common.a65
	.)

